@Library('shared-lib') _

pipeline {
    agent any

    options {
        disableConcurrentBuilds()
    }

    parameters {
        choice(
            name: 'ACTION',
            choices: ['deploy', 'destroy'],
            description: 'Acción sobre el ambiente'
        )
        booleanParam(
            name: 'RUN_TERRAFORM',
            defaultValue: false,
            description: 'Construir infraestructura con Terraform'
        )
        booleanParam(
            name: 'RUN_ANSIBLE_BD',
            defaultValue: false,
            description: 'Restaurar base de datos PG con Ansible'
        )
        booleanParam(
            name: 'RUN_ANSIBLE_APP',
            defaultValue: false,
            description: 'Desplegar la aplicación JAVA con Ansible'
        )
    }

    environment {
        TF_VAR_access_key = credentials('hwc-access-key')
        TF_VAR_secret_key = credentials('hwc-secret-key')
        
        TF_DIR   = 'app-ecs-dew-rds/terraform'
        ANS_DIR  = 'app-ecs-dew-rds/ansible'
        DUMP_DB  = 'obs://obs-alejandro-db-dumps/pg-cliente-dump.sql'
        APP_FILE = 'obs://obs-alejandro-apps/app-basic-java-0.0.1-SNAPSHOT.jar'
        REGION   = 'la-south-2'
    }

    stages {

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Terraform Deploy') {
            when {
                expression { params.RUN_TERRAFORM }
            }
            steps {
                script {

                    terraformDeploy(
                        dir: env.TF_DIR,
                        varsFile: 'local.tfvars',
                        action: params.ACTION,
                        outputs: [
                            'ecs_public_ip', 
                            'rds_public_ip',
                            'rds_postgres_password',
                            'ecs_private_key'
                        ] 
                    )

                }
            }
        }  

        stage('Restore Database') {
            when {
                allOf {
                    expression { params.ACTION == 'deploy' }
                    expression { params.RUN_ANSIBLE_BD }
                }
            }
            steps {
                script {

                    // Leer outputs generados por terraformDeploy
                    env.rds_public_ip = readFile("${WORKSPACE}/rds_public_ip.txt").trim()
                    env.rds_postgres_password = readFile("${WORKSPACE}/rds_postgres_password.txt").trim()

                    ansibleRestoreDb(
                        dir: env.ANS_DIR,
                        dump: env.DUMP_DB,
                        dbHost: env.rds_public_ip,
                        dbPassword: env.rds_postgres_password,
                        accessKeyCred: 'hwc-access-key',
                        secretKeyCred: 'hwc-secret-key'
                    )
                }
            }
        }

        stage('Helm Deploy Application') {
            when {
                allOf {
                    expression { params.ACTION == 'deploy' }
                    expression { params.RUN_ANSIBLE_APP }
                }
            }
            steps {
                script {

                    env.ecs_public_ip         = readFile("${WORKSPACE}/ecs_public_ip.txt").trim()
                    env.ecs_private_key_path  = "${WORKSPACE}/ecs_private_key.txt"

                    dir(env.ANS_DIR) {
                        
                        sh """
                        chmod 600 ${env.ecs_private_key_path}
                        set -eo pipefail
                        export HOME=\$(mktemp -d)
                        export ANSIBLE_HOST_KEY_CHECKING=False

                        echo "Configurando obsutil..."
                        obsutil config \
                        -i "$TF_VAR_access_key" \
                        -k "$TF_VAR_secret_key" \
                        -e obs.${env.REGION}.myhuaweicloud.com

                        echo "Descargando app desde OBS..."
                        obsutil cp "${env.APP_FILE}" app.jar -f

                        echo "Ejecutando playbook de despliegue del app..."
                        ansible-playbook \
                        -i inventory/hosts \
                        -e "ansible_host=${env.ecs_public_ip}" \
                        -e "ansible_user=root" \
                        --private-key ${env.ecs_private_key_path} \
                        playbooks/deploy-java.yml

                        echo "Despliegue finalizado correctamente"
                        """
                    }
                }
            }
        }

    }

    post {
        success {
            echo "Pipeline ejecutado correctamente (${params.ACTION})"
        }
        failure {
            echo "Error en el pipeline"
        }
    }
}
